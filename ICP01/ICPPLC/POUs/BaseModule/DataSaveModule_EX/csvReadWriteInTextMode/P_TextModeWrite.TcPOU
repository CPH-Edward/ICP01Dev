<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.4">
  <POU Name="P_TextModeWrite" Id="{5e1ba4d1-3ecc-4685-9715-561267f1e17c}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK P_TextModeWrite
VAR_IN_OUT
	database		: ARRAY[0..gMAX_CSV_ROWS, 0..gMAX_CSV_COLUMNS ] OF STRING(30);
	csv_rows		: UDINT;
	csv_columns		: UDINT;
END_VAR
VAR_INPUT
	bWrite			: BOOL := FALSE;(* Rising edge starts program execution *)	
	sFileName		: T_MaxString := '';(* CSV destination file path and name *)
	sFileNameOld	:T_MaxString := '';(*旧文件名*)
END_VAR
VAR_OUTPUT
	bBusy			: BOOL;
	error			: BOOL;
	nErrId			: UDINT;	
END_VAR
VAR
	bError			: BOOL;
	sNetId			: T_AmsNetId := '';	(* TwinCAT system network address *)

	sCSVLine		: T_MaxString := '';(* Single CSV text line (row, record), we are using string as record buffer (your are able to see created fields) *)
	sCSVField		: T_MaxString := '';(* Single CSV field value (column, record field) *)
	//sCSVField		: UDINT;

	nRow	 		: UDINT 	:= 0;(* Row number (record) *)
	nColumn			: UDINT 	:= 0;(* Column number (record field) *)
	hFile			: UINT		:= 0;(* File handle of the source file *)
	step			: DWORD 	:= 0;
	fbFileOpen		: FB_FileOpen;(* Opens file *)
	fbFileClose		: FB_FileClose;(* Closes file *)
	fbFilePuts		: FB_FilePuts;(* Writes one record (line) *)
	fbFileWrite     : FB_FileWrite;
	fbWriter		: FB_CSVMemBufferWriter;(* Helper function block used to create CSV data bytes (single record line) *)
    StringModelWrite		: StringModel_256000_EX;
	fbFileRename			: FB_FileRename;//修改文件名称
	fbFileDelete			: FB_FileDelete;//删除文件
	writeTON				: TON;//延时写入

	(* :=[ (* Source PLC database *)
	'0_0', '0_1', '0_2', '0_3', '0_4', '0_5',
	'1_0', '1_1', '1_2', '1_3', '1_4', '1_5',
	'2_0', '2_1', '2_2', '2_3', '2_4', '2_5',
	'3_0', '3_1', '3_2', '3_3', '3_4', '3_5',
	'4_0', '4_1', '4_2', '4_3', '4_4', '4_5',
	'5_0', '5_1', '5_2', '5_3', '5_4', '5_5'];*)

END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[DEFAULT_CSV_FIELD_SEP:= 16#2C;  //配置CSV文件的分割符，16#2C表明各个参数之间是用逗号分隔的。
CASE step OF
	0:(* Wait for rising edge at bWrite variable *)
		IF bWrite THEN
			bWrite 	:= FALSE;
			bBusy 	:= TRUE;
			error	:= FALSE;
			bError	:= FALSE;
			nErrId	:= 0;
			hFile	:= 0;
			nRow	:= 0;
			nColumn	:= 0;
			step 	:= 1;
		END_IF
	1:
		StringModelWrite.init();
		fbFileDelete(bExecute:= FALSE);
		fbFileRename(bExecute:= FALSE);
		step:=2;
	2:
		fbFileDelete(
			sNetId:= , 
			sPathName:= sFileNameOld, 
			ePath:= PATH_GENERIC, 
			bExecute:= TRUE, 
			tTimeout:= DEFAULT_ADS_TIMEOUT, 
			bBusy=> , 
			bError=> , 
			nErrId=> );
		step:=3;
	3:
		fbFileDelete(bExecute:= FALSE);
		IF NOT fbFileDelete.bBusy THEN
			step:=4;
		END_IF
	4:
		fbFileRename(
			sNetId:= , 
			sOldName:= sFileName, 
			sNewName:= sFileNameOld, 
			ePath:= PATH_GENERIC, 
			bExecute:= TRUE, 
			tTimeout:= DEFAULT_ADS_TIMEOUT, 
			bBusy=> , 
			bError=> , 
			nErrId=> );
		step:=5;
	5:
		fbFileRename(bExecute:= FALSE);
		IF NOT fbFileRename.bBusy THEN
			step:=10;
		END_IF

	10:(* Open source file *)
		fbFileOpen(  bExecute := FALSE  );
		fbFileOpen( sNetId := sNetId, sPathName := sFileName, nMode := FOPEN_MODEWRITE OR FOPEN_MODETEXT,(* Open file in TEXT mode! *)
					ePath := PATH_GENERIC, bExecute := TRUE );
		step := 20;

	20:(* Wait until open not busy *)
		//StringModelWrite.init();
		fbFileOpen( bExecute := FALSE, bError => bError, nErrID => nErrID, hFile => hFile );
		IF NOT fbFileOpen.bBusy THEN
			IF NOT fbFileOpen.bError THEN
				step := 30;
			ELSE(* Error: file not found? *)
				step := 1000;
			END_IF
		END_IF
	
	30:(* Convert one PLC record to CSV format *)
		FOR nRow:=0 TO csv_rows BY 1 DO
				sCSVLine := '';
				fbWriter.eCmd := eEnumCmd_First;(* Write first field value *)
			FOR nColumn := 0 TO csv_columns BY 1 DO
				sCSVField := STRING_TO_CSVFIELD( database[ nRow, nColumn ], FALSE );(* TODO: Get field value from your application *)
				(* Add new field to the record buffer *)
				fbWriter( 	pBuffer := ADR( sCSVLine ), cbBuffer := SIZEOF( sCSVLine ) - 1, putValue := sCSVField, pValue := 0, cbValue := 0,
							bCRLF := ( nColumn = csv_columns ) );(* bCRLF == TRUE => Write CRLF after the last field value *)
				IF fbWriter.bOk THEN
					fbWriter.eCmd := eEnumCmd_Next;(* Write next field value *)
				ELSE(* Error *)
					step := 1000;
					RETURN;
				END_IF
			END_FOR
			(* FB_FilePuts adds allready CR (carriage return) to the written line.
			We have to replace the $R$L characters with $L character to avoid double CR. *)
			IF RIGHT( sCSVLine, 2 ) = '$R$L' THEN
				sCSVLine := REPLACE( sCSVLine, '$L', 2, LEN( sCSVLine ) - 1 );
			END_IF
            StringModelWrite.append(str:= sCSVLine);
		END_FOR
		IF nRow> csv_rows THEN
			step := 40;(* Write record to the file *)
		END_IF
		writeTON(IN:=FALSE);

	40:(* Write record to file *)
		fbFileWrite( bExecute := FALSE );
		writeTON(IN:=NOT writeTON.Q,PT:=T#50MS);
		IF writeTON.Q THEN
			//fbFileWrite( sNetId := sNetId, hFile := hFile, pWriteBuff := StringModelWrite.getAddr, cbWriteLen := StringModelWrite.length, bExecute := TRUE );
			fbFileWrite( sNetId := sNetId, hFile := hFile, pWriteBuff := ADR(StringModelWrite.b), cbWriteLen := StringModelWrite.length, bExecute := TRUE );
			step := 50;
		END_IF

	50:(* Wait until write not busy *)
		fbFileWrite( bExecute := FALSE, bError => bError, nErrID => nErrID );
		IF NOT fbFileWrite.bBusy THEN
			IF NOT fbFileWrite.bError THEN
				step := 100;(* Write next record *)
			ELSE(* Error *)
				step := 1000;
			END_IF
		END_IF
		
	100:(* Close source file *)
		fbFileClose( bExecute := FALSE );
		fbFileClose( sNetId := sNetId, hFile := hFile, bExecute := TRUE );
		step := 110;

	110:(* Wait until close not busy *)
		fbFileClose( bExecute := FALSE, bError => bError, nErrID => nErrID );
		IF NOT fbFileClose.bBusy THEN
			hFile := 0;
			step := 1000;
		END_IF

	1000:(* Error or ready step => cleanup *)
		IF ( hFile <> 0 ) THEN
			step := 100; (* Close the source file *)
		ELSE
			bBusy := FALSE;
			error := bError;
			step := 0;	(* Ready *)
		END_IF
END_CASE



]]></ST>
    </Implementation>
    <LineIds Name="P_TextModeWrite">
      <LineId Id="3" Count="6" />
      <LineId Id="176" Count="0" />
      <LineId Id="10" Count="5" />
      <LineId Id="144" Count="0" />
      <LineId Id="244" Count="0" />
      <LineId Id="167" Count="0" />
      <LineId Id="172" Count="2" />
      <LineId Id="177" Count="7" />
      <LineId Id="175" Count="0" />
      <LineId Id="187" Count="2" />
      <LineId Id="186" Count="0" />
      <LineId Id="190" Count="2" />
      <LineId Id="146" Count="8" />
      <LineId Id="145" Count="0" />
      <LineId Id="194" Count="1" />
      <LineId Id="197" Count="2" />
      <LineId Id="196" Count="0" />
      <LineId Id="16" Count="43" />
      <LineId Id="249" Count="0" />
      <LineId Id="60" Count="2" />
      <LineId Id="246" Count="1" />
      <LineId Id="63" Count="2" />
      <LineId Id="248" Count="0" />
      <LineId Id="66" Count="30" />
      <LineId Id="98" Count="3" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>